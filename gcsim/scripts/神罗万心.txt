# 推荐充能：绫华：110-130%，罗莎莉亚：140-160%，心海：180-200%，万叶：任意。
# 强烈推荐罗莎莉亚携带西风枪，携带其他武器基本失去罗莎莉亚入队的意义。
# 如果罗莎莉亚携带非西风枪武器，万叶则推荐携带高精炼西风剑或西福斯的月光。

ayaka char lvl=90/90 cons=0 talent=9,9,9;
ayaka add weapon="finaleofthedeep" refine=5 lvl=90/90;
ayaka add set="blizzardstrayer" count=4;
ayaka add stats hp=4780 atk=311 atk%=0.466 cryo%=0.466 cd=0.622;
ayaka add stats atk%=0.4 er=0.11 cr=0.231 cd=0.726; 

rosaria char lvl=90/90 cons=6 talent=9,9,9;
rosaria add weapon="favoniuslance" refine=5 lvl=90/90;
rosaria add set="noblesseoblige" count=4;
rosaria add stats hp=4780 atk=311 atk%=0.466 cryo%=0.466 cr=0.311;
rosaria add stats atk%=0.4 er=0.11 cr=0.198 cd=0.792;

kazuha char lvl=90/90 cons=0 talent=9,9,9;
kazuha add weapon="xiphosmoonlight" refine=1 lvl=90/90;
kazuha add set="viridescentvenerer" count=4;
kazuha add stats hp=4780 atk=311 em=187 em=187 em=187;
kazuha add stats er=0.44 em=160 cr=0.264 cd=0.132;

kokomi char lvl=90/90 cons=0 talent=9,9,9;
kokomi add weapon="thrillingtalesofdragonslayers" refine=5 lvl=90/90;
kokomi add set="tenacityofthemillelith" count=4;
kokomi add stats hp=4780 atk=311 er=0.518 hydro%=0.466 heal=0.359;
kokomi add stats hp%=0.3 atk%=0.3 er=0.44;

#词条分布
#         hp%  atk%  em  er  cr cd
#ayaka     0    8    0   2   11  7
#rosaria   0    8    0   2   6   12
#kazuha    0    0    8   8   8   2
#kokomi    6    6    0   8   0   0

# 模拟当前充能最差环境, 12-3 水形幻人, 每 33% 血量掉 3 元素微粒, 对于非水系角色等于 1.5 微粒
target lvl=100 resist=0.1 radius=2 pos=0,2.4 hp=3050301 particle_threshold=1016767 particle_drop_count=1.5;
options swap_delay=12 iteration=1000;

fn ayaka_on_field() {
  ayaka dash;
  if .ayaka.skill.ready { ayaka skill; }
  ayaka attack, charge;
  if .ayaka.skill.ready { ayaka skill; }
  ayaka attack, charge;
  if .ayaka.skill.ready { ayaka skill; }
}

fn recharge() {
  if .rosaria.skill.ready { 
    rosaria skill; 
    ayaka dash, attack, charge;
  } else {
    if .kazuha.skill.ready { kazuha skill, high_plunge; }
    ayaka_on_field();
  }
}

active rosaria;

while 1 {
  switch 1 {
    case .rosaria.burst.ready && .rosaria.skill.ready: {
      rosaria skill, burst;
    }
    case .rosaria.burst.ready && !.rosaria.skill.ready: {
      rosaria burst;
    }
    case !.rosaria.burst.ready && .rosaria.skill.ready: {
      rosaria skill;
      # 有时循环过快偶尔会出现进入下轮循环时罗 e 好了 q 没好, 但是用完 e, q 又好了的情况
      if .rosaria.burst.ready { 
        rosaria burst; 
      } else {
        ayaka attack, charge;
      }
    }
    case !.rosaria.burst.ready && !.rosaria.skill.ready: {
      ayaka dash, attack, charge;
    }
  }
  
  if .kazuha.skill.ready { kazuha skill, high_plunge; }
  kokomi skill, dash;
  ayaka dash, skill, attack, burst;
  rosaria skill;
  ayaka dash, attack, charge, attack, charge;
  kazuha skill, high_plunge;
  if .kokomi.burst.ready { 
    kokomi burst; 
    ayaka skill, dash, attack, charge, attack:2, charge;
  } else {
    # 心海没开大的话绫华 e 要后置一点, 后面正常 az
    ayaka dash, skill, attack, charge, attack, charge, dash, attack, charge;
  }
  
  
  while .ayaka.energy + 10 < .ayaka.energymax {
    recharge();
  }
}

