# 推荐充能：雷电将军：190-210%，夜兰：130-140%，芙宁娜：120-130%，琴：180-200%
# 强烈推荐琴携带西风剑，如果携带其他武器，则其他三个队友需要在以上推荐基础上增加 20% 充能。

raiden char lvl=90/90 cons=0 talent=9,9,9;
raiden add weapon="thecatch" refine=5 lvl=90/90;
raiden add set="eosf" count=5;
raiden add stats hp=4780 atk=311.0 atk%=0.466 atk%=0.466 cr=0.311;
raiden add stats atk%=0.4 er=0.11 em=40 cr=0.198 cd=0.792;

yelan char lvl=90/90 cons=0 talent=9,9,9 +params=[breakthrough=1];
yelan add weapon="stringless" refine=5 lvl=90/90;
yelan add set="emblemofseveredfate" count=5;
yelan add stats hp=4780 atk=311 hp%=0.466 hp%=0.466 cr=0.311 ;
yelan add stats hp%=0.3 er=0.22 em=40 cr=0.198 cd=0.792;	

furina char lvl=90/90 cons=0 talent=9,9,9;
furina add weapon="wolffang" refine=1 lvl=90/90;
furina add set="goldentroupe" count=5;
furina add stats hp=4780 atk=311 hp%=0.466 hp%=0.466 cd=0.622;
furina add stats hp%=0.2 er=0.33 em=40 cr=0.297 cd=0.594;

jean char lvl=90/90, cons=0, talent=9,9,9;
jean add weapon="favoniussword" refine=5 lvl=90/90;
jean add set="viridescentvenerer" count=5;
jean add stats hp=4780 atk=311 atk%=0.466 atk%=0.466 heal=0.359;
jean add stats atk%=0.4 er=0.22 em=160 cr=0.264 cd=0.132;

#词条分布
#        hp%  atk%  em  er  cr  cd
#raiden   0    8    2   2   6   12
#yelan    6    0    2   4   6   12
#furina   4    0    2   6   9   9
#jean     0    8    8   4   8   2

# 模拟当前充能最差环境, 12-3 水形幻人, 每 33% 血量掉 3 元素微粒, 对于非水系角色等于 1.5 微粒
target lvl=100 resist=0.1 radius=2 pos=0,2.4 hp=3050301 particle_threshold=1016767 particle_drop_count=1.5;
options swap_delay=12 iteration=1000;

active raiden;
raiden skill;

if .yelan.cons < 1 {
  while 1 {
    jean skill;
    furina skill, dash, burst;
    yelan burst, attack;
    jean attack, burst, attack, skill;
    yelan attack;
    while !.yelan.skill.ready { 
      if .yelan.breakthrough {
        yelan aim;
      } else {
        yelan attack;
      }
    }
    yelan skill;
    raiden burst, attack:5, dash, attack:5, dash, attack:5, skill;
    yelan skill, attack;
  }
} else {
  while 1 {
    furina skill, dash, burst;
    jean skill;
    yelan skill, burst, attack, skill, attack;
    jean burst, attack, skill;
    raiden burst, attack:5, dash, attack:5, dash, attack:5, skill;
  }
}