kazuha char lvl=90/90 cons=0 talent=9,9,9;
kazuha add weapon="favoniussword" refine=5 lvl=90/90;
kazuha add set="viridescentvenerer" count=5;
kazuha add stats hp=4780 atk=311 em=187 em=187 em=187;
kazuha add stats er=0.44 em=160 cr=0.264 cd=0.132;

tartaglia char lvl=90/90 cons=0 talent=9,9,9;
tartaglia add weapon="theviridescenthunt" refine=1 lvl=90/90;
tartaglia add set="heartofdepth" count=2;
tartaglia add set="shimenawasreminiscence" count=2;
tartaglia add stats hp=4780 atk=311 atk%=0.466 hydro%=0.466 cd=0.622;
tartaglia add stats atk%=0.1 er=0.3 em=80 cr=0.396 cd=0.396;

xiangling char lvl=90/90 cons=6 talent=9,9,9;
xiangling add weapon="thecatch" refine=5 lvl=90/90;
xiangling add set="emblemofseveredfate" count=5;
xiangling add stats hp=4780 atk=311 er=0.518 pyro%=0.466 cr=0.311;
xiangling add stats atk%=0.2 er=0.11 em=120 cr=0.198 cd=0.792;

bennett char lvl=90/90 cons=6 talent=9,9,9; 
bennett add weapon="thealleyflash" refine=1 lvl=90/90;
bennett add set="noblesseoblige" count=4;
bennett add stats hp=3571 atk=232 er=0.518 hp%=0.466 heal=0.359;
bennett add stats hp%=0.3 er=0.55 cr=0.264 cd=0.132;

#词条分布
#          hp%  atk%  em  er  cr  cd
#kazuha     0    0    8   8   8   2
#tartaglia  0    2    4   6   12  6
#xiangling  0    4    6   2   6   12
#bennett    6    0    0   10  8   2

# 模拟当前充能最差环境, 12-3 水形幻人, 每 33% 血量掉 3 元素微粒, 对于非水系角色等于 1.5 微粒
target lvl=100 resist=0.1 radius=2 pos=0,2.4 hp=3050301 particle_threshold=1016767 particle_drop_count=1.5;
options swap_delay=12 iteration=1000;

fn recharge() {
  bennett attack; 
  while .bennett.skill.cd { bennett attack; } 
  bennett skill;
  xiangling attack:2; 
}

# 24 秒轴
active tartaglia;
while 1 {
  if .tartaglia.skill.ready {
    tartaglia walk, skill;
    bennett burst;
  } else {
    bennett burst, skill;
    tartaglia aim;
  }
  if .kazuha.burst.ready {
    kazuha burst;
    kazuha skill, high_plunge;
  } else {
    kazuha skill[hold=1], high_plunge;
  }
  xiangling burst, skill, dash;
  if .tartaglia.burst.ready { tartaglia burst; } 
  tartaglia skill, attack:2, charge, dash,
            attack:2, charge, dash,
            attack:2, charge, dash,
            attack:2, charge, dash,
            attack:2, charge, dash,
            attack:2, charge;
  kazuha skill, high_plunge;
  while .xiangling.skill.cd { xiangling attack; }
  xiangling skill, dash;
  bennett skill;
  xiangling attack:3;
  kazuha skill, high_plunge;

  while .xiangling.energy + 25 < .xiangling.energymax  {
    recharge();
  }
}