# 推荐充能：那维莱特：110-120%，芙宁娜：160-170%，万叶，钟离：无。
# 强烈推荐钟离携带西风枪，如果钟离携带其他武器，则那维莱特和芙宁娜需要在以上推荐基础上增加 20% 充能。

neuvillette char lvl=90/90 cons=0 talent=9,9,9;
neuvillette add weapon="sacrificialjade" refine=1 lvl=90/90;
neuvillette add set="marechausseehunter" count=4;
neuvillette add stats hp=4780 atk=311 hp%=0.466 hydro%=0.466 cd=0.622;
neuvillette add stats hp%=0.3 er=0.22 cr=0.198 cd=0.792;

furina char lvl=90/90 cons=0 talent=9,9,9;
furina add weapon="fleuvecendreferryman" refine=5 lvl=90/90;
furina add set="goldentroupe" count=4;
furina add stats hp=4780 atk=311 hp%=0.466 hp%=0.466 cd=0.622;
furina add stats hp%=0.3 er=0.22 cr=0.363 cd=0.462;

kazuha char lvl=90/90 cons=0 talent=9,9,9;
kazuha add weapon="xiphosmoonlight" refine=1 lvl=90/90;
kazuha add set="viridescentvenerer" count=5;
kazuha add stats hp=4780 atk=311 em=187 em=187 em=187;
kazuha add stats er=0.44 em=160 cr=0.264 cd=0.132;

zhongli char lvl=90/90 cons=0 talent=9,9,9;
zhongli add weapon="favoniuslance" refine=5 lvl=90/90;
zhongli add set="archaicpetra" count=4;
zhongli add stats hp=4780 atk=311 hp%=0.466 hp%=0.466 cr=0.311;
zhongli add stats hp%=0.4 er=0.1 atk%=0.1 cr=0.231 cd=0.726;

#词条分布
#            hp%  atk%  em  er  cr  cd
#neuvillette  6    0    0   4   6   12
#furina       6    0    0   4   11  7
#zhongli      8    2    0   2   7   11
#kazuha       0    0    8   8   8   2

# 模拟当前充能最差环境, 12-3 水形幻人, 每 33% 血量掉 3 元素微粒, 对于非水系角色等于 1.5 微粒
target lvl=100 resist=0.1 radius=2 pos=0,2.4 hp=3050301 particle_threshold=1016767 particle_drop_count=1.5;
options swap_delay=12 iteration=1000;

active neuvillette;

while 1 {
  neuvillette skill;
  # 这里两个 a 都是为了浪费时间对轴顺便触发西风枪
  zhongli attack, skill[hold=1], dash, attack;
  furina skill, dash; 
  if .furina.burst.ready { 
    furina burst; 
    kazuha skill, high_plunge;
  } else {
    # 不开大的话要偷个 a 不然切人 cd 也是发呆
    furina attack;
    kazuha skill, high_plunge;
    # 芙宁娜没大, 意味着充能不行, 又不能提前切那维莱特, 钟离出来 a 几下触发西风吧
    zhongli attack:4;
  }
  neuvillette charge, dash; 
  if .neuvillette.burst.ready {
    # 有大走正常 3 重击流程
    neuvillette burst;
    neuvillette skill, charge, dash;
    kazuha skill, high_plunge;
    neuvillette charge, dash, charge, dash;
  } else {
    # 无大就随便 a 几下浪费时间
    neuvillette attack:4;
    neuvillette skill;
    kazuha skill, high_plunge;
    if .neuvillette.burst.ready { 
      # e 完有大直接喷
      neuvillette burst;
      neuvillette charge:3;
    } else {
      # 还是没大的话就多打个无水滴重击
      neuvillette charge:2;
    }
  }
}