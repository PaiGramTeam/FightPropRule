# 推荐充能：绫华：125-135%，申鹤：180-200%，心海：180-200%，万叶：任意。
# 推荐队里至少有一把西风武器，一般是申鹤携带西风枪。
# 如果申鹤携带息灾，万叶则推荐携带高精炼西风剑或西福斯的月光。否则基本不可能循环，输出会骤降。

ayaka char lvl=90/90 cons=0 talent=9,9,9;
ayaka add weapon="finaleofthedeep" refine=5 lvl=90/90;
ayaka add set="blizzardstrayer" count=4;
ayaka add stats hp=4780 atk=311 atk%=0.466 cryo%=0.466 cd=0.622;
ayaka add stats atk%=0.2 er=0.33 cr=0.363 cd=0.462; 

shenhe char lvl=90/90 cons=0 talent=9,9,9;
shenhe add weapon="favoniuslance" refine=5 lvl=90/90;
shenhe add set="noblesseoblige" count=4;
shenhe add stats hp=4780 atk=311 er=0.518 atk%=0.466 atk%=0.466;
shenhe add stats atk%=0.5 er=0.33 cr=0.264 cd=0.132;

kazuha char lvl=90/90 cons=0 talent=9,9,9;
kazuha add weapon="xiphosmoonlight" refine=1 lvl=90/90;
kazuha add set="viridescentvenerer" count=4;
kazuha add stats hp=4780 atk=311 em=187 em=187 em=187;
kazuha add stats er=0.44 em=160 cr=0.264 cd=0.132;

kokomi char lvl=90/90 cons=0 talent=9,9,9;
kokomi add weapon="thrillingtalesofdragonslayers" refine=5 lvl=90/90;
kokomi add set="tenacityofthemillelith" count=4;
kokomi add stats hp=4780 atk=311 er=0.518 hydro%=0.466 heal=0.359;
kokomi add stats hp%=0.3 atk%=0.3 er=0.44;

#词条分布
#         hp%  atk%  em  er  cr cd
#ayaka     0    4    0   6   11  7
#shenhe    0    10   0   6   8   2
#kazuha    0    0    8   8   8   2
#kokomi    6    6    0   8   0   0

# 模拟当前充能最差环境, 12-3 水形幻人, 每 33% 血量掉 3 元素微粒, 对于非水系角色等于 1.5 微粒
target lvl=100 resist=0.1 radius=2 pos=0,2.4 hp=3050301 particle_threshold=1016767 particle_drop_count=1.5;
options swap_delay=12 iteration=1000;

fn ayaka_on_field() {
  ayaka dash;
  if .ayaka.skill.ready { ayaka skill; }
  ayaka attack, charge;
  if .ayaka.skill.ready { ayaka skill; }
  ayaka attack, charge;
  if .ayaka.skill.ready { ayaka skill; }
}

fn recharge() {
  if .shenhe.skill.ready { 
    shenhe skill, attack; 
    ayaka dash, attack, charge;
  } else {
    if .kazuha.skill.ready { kazuha skill, high_plunge; }
    ayaka_on_field();
  }
}

active shenhe;

while 1 {
  switch 1 {
    case .shenhe.burst.ready && .shenhe.skill.ready: {
      shenhe skill, burst;
    }
    case .shenhe.burst.ready && !.shenhe.skill.ready: {
      shenhe burst;
    }
    case !.shenhe.burst.ready && .shenhe.skill.ready: {
      shenhe skill, attack;
      # 有时循环过快偶尔会出现进入下轮循环时鹤 e 好了 q 没好, 但是用完 e, q 又好了的情况
      if .shenhe.burst.ready { 
        shenhe burst; 
      } else {
        ayaka attack, charge;
      }
    }
    case !.shenhe.burst.ready && !.shenhe.skill.ready: {
      ayaka dash, attack, charge;
    }
  }
  
  if .kazuha.skill.ready { kazuha skill, high_plunge; }
  kokomi skill, dash;
  ayaka dash, skill, attack, burst, attack:2, charge;
  shenhe skill, attack;
  ayaka dash, attack, charge;
  kazuha skill, high_plunge;
  if .kokomi.burst.ready { 
    kokomi burst; 
    ayaka skill, dash, attack, charge, attack:2, charge;
  } else {
    # 心海没开大的话绫华 e 要后置一点, 后面正常 az
    ayaka dash, skill, attack, charge, attack, charge, dash, attack, charge;
  }
  
  
  while .ayaka.energy + 10 < .ayaka.energymax {
    recharge();
  }
}