# 推荐充能: 
# 胡桃：一般 110-120%，但保证其他输出词条前提下副词条充能越多越好。
# 夜兰：0 命只推荐西风弓，170-180%，1 命 140-150%。
# 行秋：6 命祭礼或西风剑 170-180%，非 6 命 +30%，非西风或祭礼再 +30%。
# 钟离：140-150%，推荐西风，否则行秋夜兰 +10%。

hutao char lvl=90/90 cons=0 talent=9,9,9 ;
hutao add weapon="dragonsbane" refine=5 lvl=90/90;
hutao add set="crimsonwitchofflames" count=4;
hutao add stats hp=4780 atk=311 hp%=0.466 pyro%=0.466 cr=0.311;
hutao add stats hp%=0.2 atk%=0.1 em=80 er=0.11 cr=0.33 cd=0.528;

yelan char lvl=90/90 cons=0 talent=9,9,9;
yelan add weapon="favoniuswarbow" refine=5 lvl=90/90;
yelan add set="emblemofseveredfate" count=4;
yelan add stats hp=4780 atk=311 hp%=0.466 hydro%=0.4660 cr=0.311;
yelan add stats hp%=0.4 er=0.11 cr=0.198 cd=0.792;

xingqiu char lvl=90/90 cons=6 talent=9,9,9;
xingqiu add weapon="sacrificialsword" refine=5 lvl=90/90;
xingqiu add set="no" count=5;
xingqiu add stats hp=4780 atk=311 atk%=0.466 hydro%=0.466 cr=0.311;
xingqiu add stats atk%=0.3 er=0.22 em=40 cr=0.231 cd=0.726;

zhongli char lvl=90/90 cons=0 talent=9,9,9;
zhongli add weapon="favoniuslance" refine=5 lvl=90/90;
zhongli add set="tenacityofthemillelith" count=4;
zhongli add stats hp=4780 atk=311 hp%=0.466 hp%=0.466 cr=0.311;
zhongli add stats hp%=0.2 er=0.33 atk%=0.1 cr=0.231 cd=0.726;

#词条分布
#         hp%  atk%  em  er  cr cd
#hutao     4    2    4   2   10  8
#yelan     8    0    0   2   6   12
#xingqiu   0    6    2   2   7   11
#zhongli   4    2    0   6   7   11

# 模拟当前充能最差环境, 12-3 水形幻人, 每 33% 血量掉 3 元素微粒, 对于非水系角色等于 1.5 微粒
target lvl=100 resist=0.1 radius=2 pos=0,2.4 hp=3050301 particle_threshold=1016767 particle_drop_count=1.5;
options swap_delay=12 iteration=1000;

let sacsword_proc = 0;

fn hutao_onfiled() {
  hutao skill;
  if .hutao.cons < 1 {
    for let i = 0; i < 8; i = i + 1 {
      hutao attack:2, charge, jump;
    }
  } else {
    for let i = 0; i < 10; i = i + 1 {
      hutao attack:2, charge, dash;
    }
  }
}

active zhongli;
while 1 {
  zhongli skill[hold=1], dash;
  if sacsword_proc && .zhongli.burst.ready {
    zhongli burst;
  }
  if .yelan.burst.ready { yelan burst, attack; }
  if .xingqiu.burst.ready { xingqiu burst, attack; }
  while .xingqiu.skill.cd { xingqiu attack; }
  xingqiu skill, jump, attack;
  if .xingqiu.skill.ready {
    xingqiu skill, jump;
    sacsword_proc = 1;
  } else {
    sacsword_proc = 0;
  }
  # 很多时候夜兰行秋充能没堆够，那就行秋动完再看看能不能开大
  if .xingqiu.burst.ready { xingqiu burst, attack; }
  if .yelan.burst.ready { yelan burst; }
  yelan attack, skill;
  if .yelan.cons < 1 {
    if .yelan.skill.ready { yelan attack, skill; } # 以防某些用户带了祭礼弓
    hutao_onfiled();
    yelan attack, skill;
  } else {
    yelan attack, skill;
    hutao_onfiled();
  }
  if .hutao.burst.ready {
    hutao burst;
  }
}