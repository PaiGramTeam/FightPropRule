yoimiya char lvl=90/90 cons=0 talent=9,9,9;
yoimiya add weapon="rust" refine=5 lvl=90/90;
yoimiya add set="shimenawasreminiscence" count=4;
yoimiya add stats hp=4780 atk=311 em=187 pyro%=0.466 cr=0.311;
yoimiya add stats atk%=0.4 er=0.11 em=40 cr=0.198 cd=0.792;

yelan char lvl=90/90 cons=0 talent=9,9,9;
yelan add weapon="favoniuswarbow" refine=5 lvl=90/90;
yelan add set="emblemofseveredfate" count=4;
yelan add stats hp=4780 atk=311 hp%=0.466 hydro%=0.4660 cr=0.311;
yelan add stats hp%=0.3 er=0.22 cr=0.198 cd=0.792;

yunjin char lvl=90/90 cons=6 talent=9,9,9;
yunjin add weapon="favoniuslance" refine=5 lvl=90/90;
yunjin add set="huskofopulentdreams" count=4;
yunjin add stats hp=4780 atk=311 def%=0.583 def%=0.583 def%=0.583;
yunjin add stats def%=0.372 er=0.55 cr=0.264 cd=0.132;

zhongli char lvl=90/90 cons=0 talent=9,9,9;
zhongli add weapon="favoniuslance" refine=5 lvl=90/90;
zhongli add set="tenacityofthemillelith" count=4;
zhongli add stats hp=4780 atk=311 hp%=0.466 geo%=0.466 cr=0.311;
zhongli add stats hp%=0.3 atk%=0.2 er=0.11 cr=0.231 cd=0.726;

#词条分布
#           hp%  atk%  def%  em  er  cr  cd
#yoimiya     0    8     0    2   2   6   12
#yelan       6    0     0    0   4   6   12
#yunjin      0    0     6    0   10   8   2
#zhongli     6    2     0    0   4   7   11


# 模拟当前充能最差环境, 12-3 水形幻人, 每 33% 血量掉 3 元素微粒, 对于非水系角色等于 1.5 微粒
target lvl=100 resist=0.1 radius=2 pos=0,2.4 hp=3050301 particle_threshold=1016767 particle_drop_count=1.5;
options swap_delay=12 iteration=300;

print("yoimiya er: ", .yoimiya.stats.er);
print("yelan er: ", .yelan.stats.er);
print("yunjin er: ", .yunjin.stats.er);
print("zhongli er: ", .zhongli.stats.er);


let yoimiya_burst = 1;
active zhongli;
while 1 {
  zhongli skill[hold=1], dash;
  # 夜兰 1 命以下需要给两个 10s 冷却的 e 对 20s 轴
  if .yelan.cons < 1 {
    # 宵宫没开大, 钟离开个大拖拖时间
    if !yoimiya_burst && .zhongli.burst.ready { zhongli burst; }
    if .yelan.burst.ready { yelan burst, attack; }
    yunjin skill, burst, attack;
    if .yelan.burst.ready { yelan burst; }
    yelan attack;
    while !.yelan.skill.ready { 
      if .yelan.breakthrough {
        yelan aim;
      } else {
        yelan attack;
      }
    }
    yelan skill;
    yoimiya skill, attack:5, dash, attack:5, dash, attack:5, dash, attack:2;
    yelan skill, attack;
  } else {
    yelan skill; 
    while !.yelan.burst.ready { yelan attack; }
    yelan burst, attack, skill, attack;
    yunjin skill, burst, attack;
    yoimiya skill, attack:5, dash, attack:5, dash, attack:5, dash, attack:2;
  }
  if .yoimiya.burst.ready  {
    yoimiya burst;
    yoimiya_burst = 1;
  } else {
    yoimiya_burst = 0;
  }
}